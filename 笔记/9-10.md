# 笔记

## 杂项

### 反射折射计算

reflect(I,N)

I入射光方向，N顶点法向，都必须归一化，I是光源->顶点

其他大部分使用的入射都是顶点->光源

### 法线纹理采样

法线纹理存的是深度映射到[-1,1]，采样后UnpackNormal转换到[0,255]里，结果也还是法线空间下的

注意纹理本身要设置NormalMap

```
o.uv.zw = TRANSFORM_TEX(v.texcoord, _BumpMap);
float3 worldPos = float3(i.TtoW0.w, i.TtoW1.w, i.TtoW2.w);
fixed3 bump = UnpackNormal(tex2D(_BumpMap, i.uv.zw));
```

怎么把法线方向转换到世界空间下?

```
让变换矩阵的每一行和法线点乘，原理p87
bump = normalize(half3(dot(i.TtoW0.xyz, bump), dot(i.TtoW1.xyz, bump), dot(i.TtoW2.xyz, bump)));
```



## 切线空间到法线空间转换矩阵

待填

## 9阴影和光照

### 9.4.2阴影

备份的完整代码：

```
// Upgrade NOTE: replaced '_Object2World' with 'unity_ObjectToWorld'
// Upgrade NOTE: replaced 'mul(UNITY_MATRIX_MVP,*)' with 'UnityObjectToClipPos(*)'

Shader "Unity Shaders Book/Chapter 9/Attenuation And Shadow Use Build-in Functions" {
	Properties {
		_Diffuse ("Diffuse", Color) = (1, 1, 1, 1)
		_Specular ("Specular", Color) = (1, 1, 1, 1)
		_Gloss ("Gloss", Range(8.0, 256)) = 20
	}
	SubShader {
		Tags { "RenderType"="Opaque" }
		
		Pass {
			// Pass for ambient light & first pixel light (directional light)
			Tags { "LightMode"="ForwardBase" }
		
			CGPROGRAM
			
			// Apparently need to add this declaration
			#pragma multi_compile_fwdbase	
			
			#pragma vertex vert
			#pragma fragment frag
			
			// Need these files to get built-in macros
			#include "Lighting.cginc"
			#include "AutoLight.cginc"
			
			fixed4 _Diffuse;
			fixed4 _Specular;
			float _Gloss;
			
			struct a2v {
				float4 vertex : POSITION;
				float3 normal : NORMAL;
			};
			
			struct v2f {
				float4 pos : SV_POSITION;
				float3 worldNormal : TEXCOORD0;
				float3 worldPos : TEXCOORD1;
				SHADOW_COORDS(2)
			};
			
			v2f vert(a2v v) {
			 	v2f o;
			 	o.pos = UnityObjectToClipPos(v.vertex);
			 	
			 	o.worldNormal = UnityObjectToWorldNormal(v.normal);
			 	
			 	o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
			 	
			 	// Pass shadow coordinates to pixel shader
			 	TRANSFER_SHADOW(o);
			 	
			 	return o;
			}
			
			fixed4 frag(v2f i) : SV_Target {
				fixed3 worldNormal = normalize(i.worldNormal);
				fixed3 worldLightDir = normalize(UnityWorldSpaceLightDir(i.worldPos));
				
				fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;
				
			 	fixed3 diffuse = _LightColor0.rgb * _Diffuse.rgb * max(0, dot(worldNormal, worldLightDir));

			 	fixed3 viewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));
			 	fixed3 halfDir = normalize(worldLightDir + viewDir);
			 	fixed3 specular = _LightColor0.rgb * _Specular.rgb * pow(max(0, dot(worldNormal, halfDir)), _Gloss);

				// UNITY_LIGHT_ATTENUATION not only compute attenuation, but also shadow infos
				UNITY_LIGHT_ATTENUATION(atten, i, i.worldPos);
				
				return fixed4(ambient + (diffuse + specular) * atten, 1.0);
			}
			
			ENDCG
		}
	
		Pass {
			// Pass for other pixel lights
			Tags { "LightMode"="ForwardAdd" }
			
			Blend One One
		
			CGPROGRAM
			
			// Apparently need to add this declaration
			#pragma multi_compile_fwdadd
			// Use the line below to add shadows for point and spot lights
//			#pragma multi_compile_fwdadd_fullshadows
			
			#pragma vertex vert
			#pragma fragment frag
			
			#include "Lighting.cginc"
			#include "AutoLight.cginc"
			
			fixed4 _Diffuse;
			fixed4 _Specular;
			float _Gloss;
			
			struct a2v {
				float4 vertex : POSITION;
				float3 normal : NORMAL;
			};
			
			struct v2f {
				float4 pos : SV_POSITION;
				float3 worldNormal : TEXCOORD0;
				float3 worldPos : TEXCOORD1;
				SHADOW_COORDS(2)
			};
			
			v2f vert(a2v v) {
			 	v2f o;
			 	o.pos = UnityObjectToClipPos(v.vertex);
			 	
			 	o.worldNormal = UnityObjectToWorldNormal(v.normal);
			 	
			 	o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
			 	
			 	// Pass shadow coordinates to pixel shader
			 	TRANSFER_SHADOW(o);
			 	
			 	return o;
			}
			
			fixed4 frag(v2f i) : SV_Target {
				fixed3 worldNormal = normalize(i.worldNormal);
				fixed3 worldLightDir = normalize(UnityWorldSpaceLightDir(i.worldPos));
				
			 	fixed3 diffuse = _LightColor0.rgb * _Diffuse.rgb * max(0, dot(worldNormal, worldLightDir));

			 	fixed3 viewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));
			 	fixed3 halfDir = normalize(worldLightDir + viewDir);
			 	fixed3 specular = _LightColor0.rgb * _Specular.rgb * pow(max(0, dot(worldNormal, halfDir)), _Gloss);

				// UNITY_LIGHT_ATTENUATION not only compute attenuation, but also shadow infos
				UNITY_LIGHT_ATTENUATION(atten, i, i.worldPos);
			 	
				return fixed4((diffuse + specular) * atten, 1.0);
			}
			
			ENDCG
		}
	}
	FallBack "Specular"
}

```

阴影的产生来自Mesh Renderer的Cast Shadows 和 Receive Shadows属性。

但从shader的角度来说，如果需要物体接受来自其他物体的阴影，就需要在shader中对**阴影映射纹理**采样，并让采样结果参与到最终颜色的计算中。

如果需要物体向其他物体投射阴影，则需要计算他的**阴影映射纹理**，这是通过添加LightMode 为 ShadowCaster 的Pass实现的。大部分的FallBack都会最终指向内置的shader实现这一步骤。

```
#include"AutoLight.cginc"
v2f SHADOW_COORDS(2)
v2f vert(a2v v)   TRANSFER_SHADOWS(o)
a2v frag(v2f i)  fixed shadow = SHADOW_ATTENUATION(i)
这三个内置宏需要：
1.a2v的顶点是vertex
2.v2f vert(a2v v) v的命名
3.v2f的顶点是pos
最后shadow参与运算
return fixed4(ambient + (diffuse + specular) * atten * shadow, 1.0);
atten是平行光衰减因数
UNITY_LIGHT_ATTENUATION(atten, i, i.worldPos);
```

SHADOW_COORDS(2) 这个2，是由于前面定义了TEXCOORD0和TEXCOORD1

[^]: 分别是坐标和法线

，接下来需要使用第三个插值寄存器，即**阴影纹理坐标将占用TEXCOORD2**

Addtional Pass中添加阴影，前者替换后者即可计算**逐像素光照的阴影**

```reStructuredText
#pragma multi_compile_fwdadd_fullshadows   
#pragma multi_compile_fwdadd  
```



### 9.4.5透明物体的阴影

#### 透明度测试

```
FallBack "Transparent/Cutout/VertexLit"
FallBack "VertexLit"
Properties {
...
_Cutoff ("Alpha Cutoff", Range(0, 1)) = 0.5
｝
```

后者没有透明度测试，所以阴影不准确。前者效果正确，需要提供cutoff以使用，以及，为物体设置Two Sided的Cast Shadows。

#### 透明度混合

> 由于透明度混合需要关闭深度写入，对阴影纹理会产生影响，半透明物体不会参与深度图和阴影映射纹理的计算，他们不会向其他物体投射阴影，也不会接受来自其他物体的阴影。若要强制产生阴影，可以将其Fallback设置为 **VertexLit**并开启阴影投射和接收阴影选项。

## 10高级纹理

### 10.1立方体纹理

目的：根据物体在场景中位置不同，生成对应的立方体纹理。在unity内以cubemap形式存在

#### 生成cubemap

原理：一个空白的cubemap，指定场景中位置后，他的材质球就会是以指定位置为原点的观察结果。

此操作由Editor下特定脚本实现

```
using UnityEngine;
using UnityEditor;
using System.Collections;

public class RenderCubemapWizard : ScriptableWizard {
	
	public Transform renderFromPosition;
	public Cubemap cubemap;
	
	void OnWizardUpdate () {
		helpString = "Select transform to render from and cubemap to render into";
		isValid = (renderFromPosition != null) && (cubemap != null);
	}
	
	void OnWizardCreate () {
		// create temporary camera for rendering
		GameObject go = new GameObject( "CubemapCamera");
		go.AddComponent<Camera>();
		// place it on the object
		go.transform.position = renderFromPosition.position;
		// render into cubemap		
		go.GetComponent<Camera>().RenderToCubemap(cubemap);
		
		// destroy temporary camera
		DestroyImmediate( go );
	}
	
	[MenuItem("GameObject/Render into Cubemap")]
	static void RenderCubemap () {
		ScriptableWizard.DisplayWizard<RenderCubemapWizard>(
			"Render cubemap", "Render!");
	}
}
```

流程：

1.projec->Legacy->cubemap生成空白纹理

2.Gameobject->Render into Cubemap菜单生成纹理

修改前：[![sle9Sg.png](https://s3.ax1x.com/2021/01/10/sle9Sg.png)](https://imgchr.com/i/sle9Sg)

修改后：[![slZIJO.jpg](https://s3.ax1x.com/2021/01/10/slZIJO.jpg)](https://imgchr.com/i/slZIJO)

#### 反射

```
// Upgrade NOTE: replaced '_Object2World' with 'unity_ObjectToWorld'
// Upgrade NOTE: replaced 'mul(UNITY_MATRIX_MVP,*)' with 'UnityObjectToClipPos(*)'

Shader "Unity Shaders Book/Chapter 10/Reflection" {
	Properties {
		_Color ("Color Tint", Color) = (1, 1, 1, 1)
		_ReflectColor ("Reflection Color", Color) = (1, 1, 1, 1)
		_ReflectAmount ("Reflect Amount", Range(0, 1)) = 1
		_Cubemap ("Reflection Cubemap", Cube) = "_Skybox" {}
	}
	SubShader {
		Tags { "RenderType"="Opaque" "Queue"="Geometry"}
		
		Pass { 
			Tags { "LightMode"="ForwardBase" }
			
			CGPROGRAM
			
			#pragma multi_compile_fwdbase
			
			#pragma vertex vert
			#pragma fragment frag
			
			#include "Lighting.cginc"
			#include "AutoLight.cginc"
			
			fixed4 _Color;
			fixed4 _ReflectColor;
			fixed _ReflectAmount;
			samplerCUBE _Cubemap;
			
			struct a2v {
				float4 vertex : POSITION;
				float3 normal : NORMAL;
			};
			
			struct v2f {
				float4 pos : SV_POSITION;
				float3 worldPos : TEXCOORD0;
				fixed3 worldNormal : TEXCOORD1;
				fixed3 worldViewDir : TEXCOORD2;
				fixed3 worldRefl : TEXCOORD3;
				SHADOW_COORDS(4)
			};
			
			v2f vert(a2v v) {
				v2f o;
				
				o.pos = UnityObjectToClipPos(v.vertex);
				
				o.worldNormal = UnityObjectToWorldNormal(v.normal);
				
				o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
				
				o.worldViewDir = UnityWorldSpaceViewDir(o.worldPos);
				
				//视角方向关于顶点法线的反射
				o.worldRefl = reflect(-o.worldViewDir, o.worldNormal);
				
				TRANSFER_SHADOW(o);
				
				return o;
			}
			
			fixed4 frag(v2f i) : SV_Target {
				fixed3 worldNormal = normalize(i.worldNormal);
				fixed3 worldLightDir = normalize(UnityWorldSpaceLightDir(i.worldPos));		
				fixed3 worldViewDir = normalize(i.worldViewDir);		
				
				fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;
				
				fixed3 diffuse = _LightColor0.rgb * _Color.rgb * max(0, dot(worldNormal, worldLightDir));
				
				// 在对cubemap的采样中使用了反射方向，立方体采样不再使用位置，而是向量
				fixed3 reflection = texCUBE(_Cubemap, i.worldRefl).rgb * _ReflectColor.rgb;
				
				UNITY_LIGHT_ATTENUATION(atten, i, i.worldPos);
				
				// Mix the diffuse color with the reflected color
				fixed3 color = ambient + lerp(diffuse, reflection, _ReflectAmount) * atten;
				
				return fixed4(color, 1.0);
			}
			
			ENDCG
		}
	}
	FallBack "Reflective/VertexLit"
}

```

使用：修改物体mat的shader，把shader开放出来的cubemap选成事先生成的纹理

[![slmR56.png](https://s3.ax1x.com/2021/01/10/slmR56.png)](https://imgchr.com/i/slmR56)

#### 折射

```
Properties {	
		_RefractColor ("Refraction Color", Color) = (1, 1, 1, 1)
		_RefractAmount ("Refraction Amount", Range(0, 1)) = 1
		_RefractRatio ("Refraction Ratio", Range(0.1, 1)) = 0.5
｝折射颜色，比例，透射比
v2f vert(a2v v) {
				//入射方向归一化，法线方向归一化，两个介质透射比
				o.worldRefr = refract(-normalize(o.worldViewDir), normalize(o.worldNormal), _RefractRatio);
				TRANSFER_SHADOW(o);
				return o;
			}
	frag与反射一致
```

#### 菲涅尔反射

入射光线按一定比例分成反射和折射

```
vert与反射一致
fixed4 frag(v2f i) : SV_Target {
				fixed3 worldNormal = normalize(i.worldNormal);
				fixed3 worldLightDir = normalize(UnityWorldSpaceLightDir(i.worldPos));
				fixed3 worldViewDir = normalize(i.worldViewDir);
				fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;
				UNITY_LIGHT_ATTENUATION(atten, i, i.worldPos);
				fixed3 reflection = texCUBE(_Cubemap, i.worldRefl).rgb;
				//关键在于系数计算，最终作用在漫反射和反射上
				fixed fresnel = _FresnelScale + (1 - _FresnelScale) * pow(1 - dot(worldViewDir, worldNormal), 5);
				
				fixed3 diffuse = _LightColor0.rgb * _Color.rgb * max(0, dot(worldNormal, worldLightDir));
				
				fixed3 color = ambient + lerp(diffuse, reflection, saturate(fresnel)) * atten;
				
				return fixed4(color, 1.0);
			}
```



### 10.2渲染纹理

#### 镜面

调用：镜子下挂一个子物体相机，相机的输出（Target Texture）作为镜子纹理的输入（_MainTex，在shader翻转180）

```
v2f vert(a2v v) {
				v2f o;
				o.pos = UnityObjectToClipPos(v.vertex);
				o.uv = v.texcoord;
				o.uv.x = 1 - o.uv.x;
				return o;
			}
			fixed4 frag(v2f i) : SV_Target {
				return tex2D(_MainTex, i.uv);
			}
```

#### 玻璃

texCUBE在反射中使用反射方向（来源：vert计算），在折射中使用折射计算（来源：vert计算）

对于玻璃，texCUBE的方向向量来源于**切线空间到世界空间**变换得到的**片元->视点**向量

纹理有三张：MainTex玻璃的颜色，NormalMap玻璃法线贴图，CubeMap反射的环境贴图

```
Shader "Unity Shaders Book/Chapter 10/Glass Refraction" {
	Properties {...}
	SubShader {
		Tags { "Queue"="Transparent" "RenderType"="Opaque" }
		GrabPass { "_RefractionTex" }//关键
		Pass {		
			CGPROGRAM...
			v2f vert (a2v v) {
				v2f o;
				o.pos = UnityObjectToClipPos(v.vertex);
				o.scrPos = ComputeGrabScreenPos(o.pos);
				//截屏纹理在grabpass计算，这里得到当前物体在那个纹理里的采样坐标
				...
				return o;
			}
			
			fixed4 frag (v2f i) : SV_Target {		
				float3 worldPos = float3(i.TtoW0.w, i.TtoW1.w, i.TtoW2.w);
				fixed3 worldViewDir = normalize(UnityWorldSpaceViewDir(worldPos));
				//i的数据都是切线空间下的
				//因此对法线纹理采样后，得到的也是切线空间下的法线方向
				fixed3 bump = UnpackNormal(tex2D(_BumpMap, i.uv.zw));	
				float2 offset = bump.xy * _Distortion * _RefractionTex_TexelSize.xy;
				//在切线空间时就用截屏计算偏移，能体现局部的法线方向
				i.scrPos.xy = offset * i.scrPos.z + i.scrPos.xy;
				fixed3 refrCol = tex2D(_RefractionTex, i.scrPos.xy/i.scrPos.w).rgb;
				//原理4.9.3
				bump = normalize(half3(dot(i.TtoW0.xyz, bump), dot(i.TtoW1.xyz, bump), dot(i.TtoW2.xyz, bump)));
				fixed3 reflDir = reflect(-worldViewDir, bump);
				fixed4 texColor = tex2D(_MainTex, i.uv.xy);
				fixed3 reflCol = texCUBE(_Cubemap, reflDir).rgb * texColor.rgb;
				
				fixed3 finalColor = reflCol * (1 - _RefractAmount) + refrCol * _RefractAmount;
				
				return fixed4(finalColor, 1);
			}
			
			ENDCG
		}
	}
	
	FallBack "Diffuse"
}

```